plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id "com.google.firebase.firebase-perf"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) flutterVersionCode = '1'

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) flutterVersionName = '1.0'

def mapsApiKey = localProperties.getProperty('MAPS_API_KEY', '')

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
def hasKeystore = keystorePropertiesFile.exists()
if (hasKeystore) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

/*
 * FIX 1: Prevent firebase-iid vs firebase-messaging duplicate classes
 */
configurations.all {
    exclude group: "com.google.firebase", module: "firebase-iid"
}

android {
    namespace "com.ugotaxi_rajkumar.driver"
    compileSdkVersion 36

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions { jvmTarget = JavaVersion.VERSION_1_8 }

    sourceSets { main.java.srcDirs += 'src/main/kotlin' }

    buildFeatures {
        buildConfig = true
    }

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
    }

    defaultConfig {
        applicationId "com.ugotaxi_rajkumar.driver"
        minSdkVersion 26
        targetSdkVersion 36
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
        manifestPlaceholders += [MAPS_API_KEY: mapsApiKey ?: ""]
    }

    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }

    signingConfigs {
        release {
            if (hasKeystore) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            if (hasKeystore) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
                println "WARNING: android/key.properties not found. Using debug signing. Create key.properties for Play Store release."
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

flutter { source '../../' }

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.1.0"

    // ML Kit Text Recognition language modules (add only what you use)
    implementation "com.google.mlkit:text-recognition:16.0.1"
    implementation "com.google.mlkit:text-recognition-chinese:16.0.1"
    implementation "com.google.mlkit:text-recognition-devanagari:16.0.1"
    implementation "com.google.mlkit:text-recognition-japanese:16.0.1"
    implementation "com.google.mlkit:text-recognition-korean:16.0.1"
}
